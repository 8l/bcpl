/*
############### UNDER DEVELOPMENT #####################

This library provides some functions that interface with the OpenGL
Graphics library that should work with both OpenGL ES using EGL and
the full version of OpenGL using SDL. The intention is for BCPL
programs to work without change under either version of OpenGL.

This will be compiled with one of the following conditional
compilation options set.

  OpenGL       for the full OpenGL library used with SDL
  OpenGLES     for OpenGL ES in the Raspberry Pi

Implemented by Martin Richards (c) Jan 2014

Change history:

26/08/12
Initial implementation.

15/07/13
Started adding OpenGL functions.


It should typically be included as a separate section for programs that
need it. Such programs typically have the following structure.

GET "libhdr"
MANIFEST { g_glbase=nnn  }  // Only used if the default setting of 450 in
                            // libhdr is not suitable.
GET "gl.h"
GET "gl.b"                  // Insert the library source code
.
GET "libhdr"
MANIFEST { g_glbase=nnn  }  // Only used if the default setting of 450 in
                            // libhdr is not suitable.
GET "gl.h"
Rest of the program
 
*/

LET glInit() = VALOF
{ LET mes = VEC 256/bytesperword
  mes%0 := 0

  UNLESS sys(Sys_gl, GL_Init) DO
  { //sys(Sys_gl, GL_getError, mes)
    //writef("*nglInit unable to initialise OpenGL: %s*n", mes)
    RESULTIS FALSE
  }

  // Successful
  RESULTIS TRUE
}

AND glMkScreen(title, xsize, ysize) = VALOF
{ // Create an OpenGL window with given title and size
  LET mes = VEC 256/bytesperword
  mes%0 := 0

  //writef("glMkScreen: Creating an OpenGL window*n")

  screenxsize, screenysize := xsize, ysize

  //writef("MkScreen: calling sys(Sys_gl, GL_MkScreen, %s, %n %n)*n",
  //        title, xsize, ysize)

  screenxsize := sys(Sys_gl, GL_MkScreen, title, xsize, ysize)
  screenysize := result2

  writef("GL_MkScreen: returned screen size %n x %n*n",
         screenxsize, screenysize)

  UNLESS screenxsize>0 DO
  { //sys(Sys_gl, GL_GetError, mes)
    writef("Unable to create an OpenGL screen: *n", mes)
    RESULTIS 0
  }

  result2 := screenysize
  RESULTIS screenxsize
}

AND glClose() BE
{ sys(Sys_gl, GL_Quit)
}

AND glMkProg() = VALOF
{ //writef("glMkProg: entered*n")
  RESULTIS sys(Sys_gl, GL_MkProg)
}

AND glCompileVshader(prog, cstr)  = VALOF
{ // Create and compile the vertex shader whose source is
  // in the C string cstr
  //writef("glCompileVshader: entered, prog=%n cstr=%n*n", prog, cstr)
  sys(Sys_gl, GL_CompileVshader, prog, cstr)
  RESULTIS -1
}

AND glCompileFshader(prog, cstr)  = VALOF
{ // Create and compile the fragment shader whose source is
  // in the C string cstr
  //writef("glCompileFshader: entered, prog=%n cstr=%n*n", prog, cstr)
  sys(Sys_gl, GL_CompileFshader, prog, cstr)
  RESULTIS -1
}
 
AND glLinkProg(prog) = VALOF
{ //writef("glLinkProg(%n): entered*n", prog)
  RESULTIS sys(Sys_gl, GL_LinkProgram, prog)
}
 
AND glBindAttribLocation(prog, loc, name) = VALOF
{ // Specify attribute location before linking.
  //writef("glBindAttribLocation(%n, %n, %s): entered*n", prog, loc, name)
  RESULTIS sys(Sys_gl, GL_BindAttribLocation, prog, loc, name)
}
 
AND glGetAttribLocation(prog, name) = VALOF
{ // Get attribute location after linking.
  //writef("glGetAttribLocation(%n, %s): entered*n", prog, name)
  RESULTIS sys(Sys_gl, GL_GetAttribLocation, prog, name)
}
 
AND glGetUniformLocation(prog, name) = VALOF
{ // Get uniform location after linking
  //writef("glGetUniformLocation(%n, %s): entered*n", prog, name)
  RESULTIS sys(Sys_gl, GL_GetUniformLocation, prog, name)
}
 
AND glLoadModel(prog, name)  = VALOF
{ //writef("glLoadModel: %n %s entered*n", prog, name)
  RESULTIS -1
}

AND glUseProgram(prog) = VALOF
{ //writef("glUseProgram: %n entered*n", prog)
  RESULTIS  sys(Sys_gl, GL_UseProgram, prog)
}
 
AND sc3(x) = glF2N(    1_000, x)// #*    1000.0)
AND sc6(x) = glF2N(1_000_000, x)// #* 1000000.0)


AND glUniform1f(loc, x) = VALOF
{ //writef("gl.b: glUniform1f: loc=%6i x=%8.3d*n",
  //       loc, sc3(x))
  sys(Sys_gl, GL_Uniform1f, loc, x)
//abort(1000)
  RESULTIS -1
}
 
AND glUniform2f(loc, x, y) = VALOF
{ //writef("gl.b: glUniform2f: loc=%6i x=%8.3d y=%8.3d*n",
  //        loc, sc3(x), sc3(y))
  sys(Sys_gl, GL_Uniform2f, loc, x, y)
//abort(1000)
  RESULTIS -1
}
 
AND glUniform3f(loc, x, y, z) = VALOF
{ //writef("gl.b: glUniform3f: loc=%6i x=%8.3d y=%8.3d z=%8.3d*n",
  //       loc, sc3(x), sc3(y), sc3(z))
  sys(Sys_gl, GL_Uniform3f, loc, x, y, z)
//abort(1000)
  RESULTIS -1
}
 
AND glUniform4f(loc, x, y, z, w) = VALOF
{ //writef("gl.b: glUniform4f: loc=%6i x=%8.3d y=%8.3d z=%8.3d w=%8.3d*n",
  //       loc, sc3(x), sc3(y), sc3(z), sc3(w))
  sys(Sys_gl, GL_Uniform4f, loc, x, y, z, w)
//abort(1000)
  RESULTIS -1
}
 
AND glDeleteShader(shader) = VALOF
{ //writef("glDeleteShader: %n entered*n", shader)
  RESULTIS -1
}
 
AND glSwapBuffers() = VALOF
{ //writef("glSwapBuffers: entered*n")
  RESULTIS sys(Sys_gl, GL_SwapBuffers)
}
 
AND glCos(angle) = VALOF
{ // angle is fixed point in degrees with 6 decimal after
  // the decimal point. The result is a float.
  LET radians = sys(Sys_flt, fl_N2F,
                    muldiv(angle, 3_141593, 1_000000),
                    180_000000)
  RESULTIS sys(Sys_flt, fl_cos, radians)
}

AND glSin(angle) = VALOF
{ // angle is fixed point in degrees with 6 decimal after
  // the decimal point. The result is a float.
  LET radians = sys(Sys_flt, fl_N2F,
                    muldiv(angle, 3_141593, 1_000000),
                    180_000000)
  RESULTIS sys(Sys_flt, fl_sin, radians)
}

AND glSetIdent4(v) BE
{ glSetvecN2F(v, 16, 1,
              1, 0, 0, 0,
              0, 1, 0, 0,
              0, 0, 1, 0,
              0, 0, 0, 1)
}

AND glSetvec(v, n,
             n0,n1,n2,n3,n4,n5,n6,n7,n8,n9,n10,n11,n12,n13,n14,n15) BE
{ LET p = @n0
//writef("glSetvec: entered*n")
  FOR i = 0 TO n-1 DO v!i := p!i
}

// Used to set colours, points, mat3 and mat4, etc
AND glSetvecN2F(v, n, scale,
                n0,n1,n2,n3,n4,n5,n6,n7,n8,n9,n10,n11,n12,n13,n14,n15) BE
{ LET p = @n0
//writef("glSetvecN2F: entered*n")
  FOR i = 0 TO n-1 DO
    v!i := sys(Sys_flt, fl_N2F, scale, p!i)
}

AND glSetvecF2N(v, n, scale,
                f0,f1,f2,f3,f4,f5,f6,f7,f8,f9,f10,f11,f12,f13,f14,f15) BE
{ LET p = @f0
//writef("glSetvecF2N: entered*n")
  FOR i = 0 TO n-1 DO
    v!i := sys(Sys_flt, fl_F2N, scale, p!i)
}

AND glSetPerspective(mat4, aspect, fov, n, f) BE
{ // The field of view is given as a field of view at unit distance
  // ie field of view is 45 degrees if fov=2.0
  // aspect = width/height of screen in pixels
  LET fv = 2.0 #/ fov

  mat4!00 := fv #/ aspect // Column 1
  mat4!01 := 0.0
  mat4!02 := 0.0
  mat4!03 := 0.0

  mat4!04 := 0.0          // Column 2
  mat4!05 := fv
  mat4!06 := 0.0
  mat4!07 := 0.0

  mat4!08 := 0.0         // Column 3
  mat4!09 := 0.0
  mat4!10 := (f #+ n) #/ (n #- f)
  mat4!11 := #-1.0

  mat4!12 := 0.0         // Column 4
  mat4!13 := 0.0
  mat4!14 := (2.0 #* f #* n) #/ (n #- f)
  mat4!15 := 0.0
}

AND glRadius2(x, y) = VALOF
{ LET a = sys(Sys_flt, fl_mul, x, x)
  a := sys(Sys_flt, fl_add, a, sys(Sys_flt, fl_mul, y, y))
  RESULTIS sys(Sys_flt, fl_sqrt, a)
}

AND glRadius3(x, y, z) = VALOF
{ LET a = sys(Sys_flt, fl_mul, x, x)
  a := sys(Sys_flt, fl_add, a, sys(Sys_flt, fl_mul, y, y))
  a := sys(Sys_flt, fl_add, a, sys(Sys_flt, fl_mul, z, z))
  RESULTIS sys(Sys_flt, fl_sqrt, a)
}

// glN2F(1_000, 1_234) => 1.234
AND glN2F(scale, n) = sys(Sys_flt, fl_N2F, scale, n)

// glF2N(1_000, 1.234) => 1_234
AND glF2N(scale, x) = sys(Sys_flt, fl_F2N, scale, x)

// glN2Fv converts n scaled fixed point numbers in v to floating
//AND glN2Fv(v, n, scale) BE
//  FOR i = 0 TO n-1 DO v!i := sys(Sys_flt, fl_N2F, scale, v!i)

// glF2Nv converts n floating point numbers to scaled fixed point.
//AND glF2Nv(v, n, scale) BE
//  FOR i = 0 TO n-1 DO v!i := sys(Sys_flt, fl_F2N, v!i, scale)

AND glMat4mul(a, b, c) BE
{ // Perform c := a*b, where a and b are 4x4 floating point matrices
  // a,b and c need not be distinct.
//writef("glMat4mul: calling GL_M4mulM4 %n %n %n*n", a, b, c)
  sys(Sys_gl, GL_M4mulM4, a, b, c)
}

AND glMat4mulV(a, b, c) BE
{ // Perform c := a*b, where a is a 4x4 floating point matrix
  // and b and c are 4 element vectors. b and c need not be distinct.
//writef("glMat4mulV: calling GL_M4mulV %n %n %n*n", a, b, c)
  sys(Sys_gl, GL_M4mulV, a, b, c)
}

AND glUniformMatrix4fv(loc, prog, matrix) BE
{ //writef("glUniformmatrix4fv: entered*n")
  sys(Sys_gl, GL_UniformMatrix4fv, loc, prog, matrix)
}

AND glClearColour(r, g, b, a) BE
{ sys(Sys_gl, GL_ClearColour, r,g,b,a)
}

AND glClearBuffer() BE
{ sys(Sys_gl, GL_ClearBuffer)
}

AND getevent() = VALOF
{ //writef("gl: Calling sys(Sys_sdl, GL_pollevent...)*n")
  RESULTIS sys(Sys_gl, GL_pollevent, @eventtype)
}

AND loadmodel(filename, modelv) = VALOF
{ // This function reads a .mdl file specifying the vertices and
  // indices of a model. It returns TRUE if successful.
  // It updates
  // modelv!0 to point to the vertex data
  // modelv!1 to the number of values in the vertex data
  // modelv!2 to point to the index data packed as 16-bit values
  // modelv!3 to the number of 16-bit values in the index data.

  LET res = TRUE
  LET stdin = input()
  LET instream = findinput(filename)
  LET scale = 1_000 // The default scale
                    // It is the scaled fixed point value representing 1.000
  LET vdata = TRUE  // Initially reading vertex data by default
  // Declare self expanding vectors for the
  LET vvec, vp, vupb = 0, -1, -1 // vertices and
  LET ivec, ip, iupb = 0, -1, -1 // indices.

  UNLESS instream DO
  { writef("Trouble with file %s*n", filename)
    RESULTIS FALSE
  }

  selectinput(instream)

  ch := rdch()
  lineno := 1 // The first line has lineno=1

nxt:
  lex()

  SWITCHON token INTO
  { DEAFAULT:  writef("line %n: Bad model file*n", lineno)
               res := FALSE
               GOTO ret

    CASE s_scale:         // s scale
      lex()
      UNLESS token = s_num DO
      { writef("Line %n: Bad Scale statement*n", lineno)
        res := FALSE
        GOTO ret
      }
      scale := lexval
      GOTO nxt

    CASE s_vertex:
      vdata := TRUE
      GOTO nxt

    CASE s_index:
      vdata := FALSE
      GOTO nxt

    CASE s_num:
      TEST vdata
      THEN pushf(@vvec, glN2F(scale, lexval))
      ELSE pushi(@ivec, lexval)
      GOTO nxt

    CASE s_eof:
      ENDCASE
  }

  modelv!0, modelv!1 := vvec, vp+1
  modelv!2, modelv!3 := ivec, ip+1

//writef("Model %s*n", filename, vp+1, ip+1)
//writef("VertexData= %i7 VertexDataSize= %i4*n", vvec, vp+1)
//writef("IndexData = %i7 IndexDataSize = %i4*n", ivec, ip+1)

ret:
  IF instream DO endstream(instream)
  selectinput(stdin)
  RESULTIS res
}

AND lex() BE
{ LET neg = FALSE

  SWITCHON ch INTO
  { DEFAULT:
      writef("line %n: Bad character '%c' in model file*n", lineno, ch)
      ch := rdch()
      LOOP

    CASE 'z':            // A debugging aid
    CASE endstreamch:
      token := s_eof
      RETURN

    CASE '/': // Skip over comments
      UNTIL ch='*n' | ch=endstreamch DO ch := rdch()
      LOOP

    CASE '*n':
      lineno := lineno+1

    CASE '*s':
      ch := rdch()
      LOOP

    CASE 's':
      token := s_scale
      ch := rdch()
      RETURN

    CASE 'v':
      token := s_vertex
      ch := rdch()
      RETURN

    CASE 'i':
      token := s_index
      ch := rdch()
      RETURN

    CASE '-':
      neg := TRUE
    CASE '+':
      ch := rdch()

    CASE '0': CASE '1': CASE '2': CASE '3': CASE '4': 
    CASE '5': CASE '6': CASE '7': CASE '8': CASE '9':
      lexval := 0
      WHILE '0'<=ch<='9' DO
      { lexval := 10*lexval + ch - '0'
        ch := rdch()
      }
      IF neg DO lexval := - lexval
      token := s_num
      RETURN
  }
} REPEAT

AND pushf(v, val) BE
{ // v is a self expanding vector of 32-bit values
  // v -> [data, p, upb]
  // Initially data=0, p=-1 and upb=-1
  // If p>=0 data!p holds the latest value pushed into v
  // When necessary, a new larger vector data is allocated
  // initialised with the content of the previous vector.
  LET data, p, upb = v!0, v!1, v!2

  IF p=upb DO
  { // We must expand the vector
    LET newupb = (upb+10)*3/2
    LET newdata = getvec(newupb)
    UNLESS newdata DO
    { writef("More memory needed*n")
      abort(999)
    }
    FOR i = 0 TO upb DO newdata!i := data!i
    FOR i = upb+1 TO newupb DO newdata!i := 0
    IF data DO freevec(data)
    v!0, v!2 := newdata, newupb
    data := newdata
  }
  p := p+1
  data!p, v!1 := val, p
  //writef("pushf: %i3 %x8*n", p, val)
}

AND pushi(v, val) BE
{ // v is a self expanding vector of 16-bit values
  // v -> [data, p, upb]
  // Initially data=0, p=-1 and upb=-1
  // data has room for upb+1 16-bit values
  // If p>=0 get16(data,p) holds the latest 16-bit value pushed into v
  // When necessary, a new larger vector data is allocated
  // initialised with the content of the previous vector.
  LET data, p, upb = v!0, v!1, v!2

  IF p=upb DO
  { // We must expand the vector
    LET newupb = (upb+10)*3/2 | 1 // Ensure that it is odd
    LET wupb = newupb/2
    LET newdata = getvec(wupb)
    UNLESS newdata DO
    { writef("More memory needed*n")
      abort(999)
    }
    FOR i = 0 TO p/2 DO newdata!i := data!i
    FOR i = p/2+1 TO newupb/2 DO newdata!i := 0
    IF data DO freevec(data)
    v!0, v!2 := newdata, newupb
    data := newdata
  }
  p := p+1
  v!1 := p
  put16(data, p, val)
  //writef("pushi: %i3 %i4*n", p, val)
}

AND get16(v, i) = VALOF
{ LET w = 0
  LET p = 2*i
  LET a, b = v%p, v%(p+1)
  (@w)%0 := 1
  TEST (w & 1) = 0
  THEN RESULTIS (a<<8) + b // Big ender m/c 
  ELSE RESULTIS (b<<8) + a // Little ender m/c 
}

AND put16(v, i, val) BE
{ LET w = 0
  LET p = 2*i
  LET a, b = val&255, (val>>8) & 255
  (@w)%0 := 1
  TEST (w & 1) = 0
  THEN v%p, v%(p+1) := b, a // Big ender m/c 
  ELSE v%p, v%(p+1) := a, b // Little ender m/c 
}

AND glVertexData(loc, n, stride, data) BE
  sys(Sys_gl, GL_VertexData, loc, n, stride, data)

AND glDrawTriangles(n, indexv) BE
  // n = number of index values (3 index values per triangle)
  // indexv is a vector of unsigned 16-bit integers
  sys(Sys_gl, GL_DrawTriangles, n, indexv)
